buildscript {
    ext {
        kotlinVersion = '1.3.21'
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion")
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

allprojects {

    group = 'kit'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }
}

subprojects {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

apply plugin: 'kotlin-kapt'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'

sourceCompatibility = '1.8'

dependencies {
    implementation project(':tacocloud-domain')
    implementation project(':tacocloud-api')
    implementation project(':tacocloud-data')
    implementation project(':tacocloud-security')
    implementation 'joda-time:joda-time:2.3'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    runtime 'com.h2database:h2'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    kapt 'org.springframework.boot:spring-boot-configuration-processor'
    optional 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.seleniumhq.selenium:htmlunit-driver'
    testImplementation 'org.seleniumhq.selenium:selenium-java'
}
